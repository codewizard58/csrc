; CODE BOOT LOADER
	ORG 0A000H
 	LOAD 0F400H
;
DIRBUF:DS 32
DEXT:	EQU DIRBUF+12
DRC:	EQU DIRBUF+15
DBLKN:	EQU DIRBUF+16
MAXRNO:DW 0
CURRNO:DW 0
BUFP:  DW 0
START:	DW 0
;
;
LOADFILE:PUSH HL
	DI
	LD A,(DEXT)
	LD HL,0
	SRA A
	RR L
	LD H,A
	LD A,(DRC)
	LD D,0
	LD E,A
	ADD HL,DE
	LD (MAXRNO),HL
	LD DE,0
	LD (CURRNO),DE
LOADLOOP:LD HL,(MAXRNO)
	LD DE,(CURRNO)
	AND A
	SBC HL,DE
	JR NZ,LOADNXT
	LD HL,(START)
	EI
	POP HL
	DEC HL
	LD (HL),0
	DEC HL
	LD (HL),1
	RET
	JP (HL)
LOADNXT:EX DE,HL ; HL=LOGICAL RECNO
	ADD HL,HL
	ADD HL,HL
	ADD HL,HL
	LD A,0FH
	AND H
	LD DE,DBLKN
	LD H,0
	LD L,A
	ADD HL,DE
	LD H,(HL) ; H=BLKNO
	LD A,(CURRNO)
	AND 3FH
	LD L,A
	XOR A
	RR H
	RR A
	RR H
	RR A
	RR H
	RR A
	OR L
	LD L,A  ; PHYSICAL REC-NO
;
	SRL H
	RR L
	SRL H
	RR L    ; HL= LOGICAL SECTOR
	LD DE,10
	JR DIVIDE
LOADLP1:JR LOADLOOP
;;; INTEGER DIVIDE
;
DIVIDE:PUSH HL
	LD HL,0
	EX (SP),HL
	LD BC,1
DIV1:  AND A
	SBC HL,DE
	JR C,DIV2 ; DE > HL
	ADD HL,DE ; RESTORE HL
	BIT 7,D
	JR NZ,DIV3 ; DE OVERFLOW!
	EX DE,HL
	ADD HL,HL
	EX DE,HL  ; DE=DE*2
	SLA C
	RL B	  ; BC=BC*2
	JR DIV1
DIV2:  ADD HL,DE ; RESTORE HL
DIV3:  AND A
	SBC HL,DE
	JR C,DIV4
	EX (SP),HL
	ADD HL,BC
	EX (SP),HL
	JR DIV5
DIV4:  ADD HL,DE ; RESTORE HL
DIV5:  SRL D
	RR E
	SRL B
	RR C
	LD A,B
	OR C
	JR NZ,DIV3
	POP DE	  ; DE= HL/DE
; HL=SECTOR DE=TRACK
	INC E
	INC E
; DO THE SEEK!
	LD A,1
	OUT (09FH),A
	LD A,0DAH
	OUT (05FH),A
	XOR A
	OUT (09FH),A
	LD A,E
	OUT (0BFH),A
	LD A,18H
; TYPEI
	OUT (05FH),A
	JR DSEEKL
LOADLP3:JR LOADLP1
DSEEKL:LD B,40H
DSEEKL1:DJNZ DSEEKL1
	IN A,(05FH)
	AND A
	JR Z,DSEEKL
; READ THE DATA
; L= SECTOR NUM
	LD A,L
	OUT (0DFH),A
; CALCULATE HOW MUCH TO READ
	LD HL,(MAXRNO)
	LD DE,(CURRNO)
	LD A,H
	CP D
	JR NZ,RALL
	LD A,L
	AND 0FCH
	LD L,A
	LD A,E
	AND 0FCH
	CP L
	JR NZ,RALL
	LD A,(MAXRNO)
	AND 3
	LD BC,0
	SRL A
	RR C
	LD B,A
DREAD: LD HL,200H
	AND A
	SBC HL,BC
	EX DE,HL
	JR DREAD1
RALL:	LD BC,200H
	JR DREAD
LOADLP2:JR LOADLP3
DREAD1:PUSH BC
	LD A,B
	AND A
	JR NZ,DREAD2
	INC A
DREAD2:LD B,C
	LD C,A
	LD HL,(BUFP)
;
	LD A,1
	OUT (09FH),A
	LD A,05AH
	OUT (05FH),A
	XOR A
	OUT (09FH),A
	LD A,8CH
	OUT (05FH),A
DREAD4:IN A,(0BFH)
	LD (HL),A
	INC HL
	DJNZ DREAD4
	DEC C
	JR NZ,DREAD4
	LD A,D
	AND A
	JR NZ,DREAD5
	INC D
	LD A,E
	AND A
	JR Z,DREAD6
DREAD5:LD C,D
	LD B,E
DREAD7:IN A,(0BFH)
	DJNZ DREAD7
	DEC C
	JR NZ,DREAD7
DREAD6:LD B,40H
DREAD8:DJNZ DREAD8
	IN A,(05FH)
	AND 09FH
	JR NZ,DREAD9
	LD (BUFP),HL
	POP BC ; BYTE COUNT
	XOR A
	RL C
	RL B   ; BC=NUM BYTES READ
	LD C,B
	LD B,0 ;
	LD HL,(CURRNO)
	ADD HL,BC
	LD (CURRNO),HL
	JR LOADLP2
DREAD9:POP BC
; RETURN ERROR CODE.
	POP HL
	LD DE,0
	DEC HL
	LD (HL),D
	DEC HL
	LD (HL),E
	RET
	END

